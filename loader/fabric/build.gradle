import dex.plugins.outlet.v2.util.ReleaseType
import xyz.jpenilla.resourcefactory.fabric.Environment

plugins {
    id 'multiloader-fabric'
}

allprojects {
    fabricModJson {
        mitLicense()
        author("OffsetMonkey538")
        contact {
            extra = ["discord": "https://discord.offsetmonkey538.top"]
        }
        environment = Environment.ANY
        mainEntrypoint("top.offsetmonkey538.loottablemodifier.fabric.platform.FabricPlatformMain")
        if(project.hasProperty("fabricDatagenEntrypoint")) entrypoint("fabric-datagen", project.findProperty("fabricDatagenEntrypoint"))
        mixin("loot-table-modifier.modded.mixins.json")
        mixin("loot-table-modifier.version.modded.mixins.json")
        mixin("loot-table-modifier.version.loader.mixins.json")

        depends("fabric-api", "*")
        depends("monkeylib538", ">=${rootProject.monkeylib538_version}")
    }

    // Gotta have this here cause I can't import it in buildSrc
    outlet.allowedReleaseTypes = Set.of(ReleaseType.RELEASE)

    dependencies {
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fapi_version}"

        modCompileOnly("top.offsetmonkey538.monkeylib538:monkeylib538-fabric:${rootProject.monkeylib538_version}+fabric") {
            exclude(group: "net.fabricmc.fabric-api")
        }
    }
}

subprojects {
    dependencies {
        modImplementation("top.offsetmonkey538.monkeylib538:monkeylib538-fabric-${project.monkeylib538_suffix}:${rootProject.monkeylib538_version}+fabric+${project.monkeylib538_suffix}") {
            exclude(group: "net.fabricmc.fabric-api")
        }
    }

    loom {
        runs {
            datagenClient {
                name = "${project.project_name} Datagen"
                inherit client
                runDir "build/datagen"
                vmArg "-Dfabric-api.datagen"
                vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
                vmArg "-Dfabric-api.datagen.modid=loot-table-modifier"
                vmArg "-Ddevauth.enabled=false"
                ideConfigGenerated(true)
            }
        }
    }

    // TODO: breaks resource-factory plugin: sourceSets {
    // TODO: breaks resource-factory plugin:     main {
    // TODO: breaks resource-factory plugin:         resources {
    // TODO: breaks resource-factory plugin:             srcDirs += [
    // TODO: breaks resource-factory plugin:                     "src/main/generated"
    // TODO: breaks resource-factory plugin:             ]
    // TODO: breaks resource-factory plugin:         }
    // TODO: breaks resource-factory plugin:     }
    // TODO: breaks resource-factory plugin: }

    modrinth {
        dependencies {
            required.project "fabric-api"
            required.project "monkeylib538"
        }
    }
}
