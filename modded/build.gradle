plugins {
    id 'multiloader-modded'
    id 'fabric-loom'
}

allprojects {
    apply plugin: "multiloader-modded"
    apply plugin: "fabric-loom"

    repositories {
        exclusiveContent {
            forRepository {
                maven {
                    name = "Sponge"
                    url = "https://repo.spongepowered.org/repository/maven-public"
                }
            }
            filter {
                includeGroupAndSubgroups "org.spongepowered"
            }
        }
    }

    dependencies {
        compileOnly project(":common")
        commonJava project(path: ":common", configuration: "commonJava")
        commonResources project(path: ":common", configuration: "commonResources")

        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.officialMojangMappings()

        compileOnly "org.spongepowered:mixin:0.8.5"
        compileOnly annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.5")

        modCompileOnly "top.offsetmonkey538.monkeylib538:monkeylib538-modded:${rootProject.monkeylib538_version}+modded"
    }
}

subprojects {
    dependencies {
        compileOnly project(":modded")
        commonJava project(path: ":modded", configuration: "commonJava")
        commonResources project(path: ":modded", configuration: "commonResources")

        modCompileOnly "top.offsetmonkey538.monkeylib538:monkeylib538-modded-${project.monkeylib538_suffix}:${rootProject.monkeylib538_version}+modded+${project.monkeylib538_suffix}"
    }
}
