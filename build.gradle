import dex.plugins.outlet.v2.util.ReleaseType

import java.nio.file.Files

plugins {
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id 'io.github.dexman545.outlet' version '1.6.1'
	id 'com.modrinth.minotaur' version '2.+'
	id 'maven-publish'
}

ext {
    versionPrefix = rootProject.mod_version
    if (!Boolean.parseBoolean(System.getenv("IS_RELEASE"))) {
        String preReleaseVersion = System.currentTimeMillis()

        if (Boolean.parseBoolean(System.getenv("PRESERVE_PRERELEASE_VERSION"))) {
            var preReleaseVersionFile = file("preReleaseVersion.txt").toPath()
            if (Files.exists(preReleaseVersionFile)) preReleaseVersion = Files.readString(preReleaseVersionFile)
            Files.writeString(preReleaseVersionFile, preReleaseVersion)
        }

        var separator = "-"
        if (versionPrefix.contains("-")) separator = "."
        versionPrefix = "${versionPrefix}${separator}${preReleaseVersion}"
    }
    final String versionSuffix = System.getenv("VERSION_SUFFIX")
    if (versionSuffix != null && !versionSuffix.isEmpty()) {
        versionPrefix = "${versionPrefix}+${versionSuffix}"
    }

    System.out.println("Version Prefix: " + versionPrefix)
}

allprojects {
    group = "top.offsetmonkey538.loottablemodifier"
    base.archivesName = "mesh-lib" // todo: move to subprojects and give em different suffixes
    version = "${rootProject.versionPrefix}+${project.property("minecraft_version")}" // todo: again, move to subprojects
}

outlet {
	maintainPropertiesFile = System.getenv("DISABLE_PROPERTIES_UPDATE") == null
	mcVersionRange = project.supported_minecraft_versions
	allowedReleaseTypes = [ReleaseType.RELEASE]
	propertiesData = [
			'fapi_version': outlet.fapiVersion(project.minecraft_version),
			'yarn_version': outlet.yarnVersion(project.minecraft_version),
			'loader_version': outlet.loaderVersion()
	]
}


loom {
	splitEnvironmentSourceSets()

	mods {
		modid {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	runs {
		server {
			runDir "run/server"
		}
		client {
			runDir "run/client"
		}
		datagenClient {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=loot-table-modifier"

			runDir "build/datagen"
		}
	}
}

// https://gist.github.com/maityyy/3dbcd558d58a6412c3a2a38c72706e8e
afterEvaluate {
	loom.runs.configureEach {
		vmArg "-javaagent:${configurations.compileClasspath.find{ it.name.contains("sponge-mixin") }}"
		if (System.getenv("DISABLE_PROPERTIES_UPDATE") == null) vmArg "-Ddevauth.enabled=true"
	}
}
sourceSets {
	main {
		resources {
			srcDirs += [
			        "src/main/generated"
			]
		}
	}
}

configurations {
	includeModImplementation

	include.extendsFrom           includeModImplementation
	modImplementation.extendsFrom includeModImplementation


	includeImplementation

	include.extendsFrom        includeImplementation
	implementation.extendsFrom includeImplementation


	includeApi

	include.extendsFrom includeApi
	api.extendsFrom     includeApi
}

repositories {
	mavenCentral()
	mavenLocal()
	maven {
		name = "DevAuth"
		url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
		content {
			includeGroup "me.djtheredstoner"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// DevAuth
	modLocalRuntime "me.djtheredstoner:DevAuth-fabric:${devauth_version}"
	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fapi_version}"

	// Uncomment for including a module of fabric api
	// includeModImplementation fabricApi.module("fabric-api-base", project.fapi_version)
}

processResources {
	final Map properties = Map<String, String>.of(
			"modVersion",                 project.mod_version,
			"supportedMinecraftVersions", project.supported_minecraft_versions
	)

	inputs.properties(properties)

	filesMatching("fabric.mod.json") {
		expand(properties)
	}

	exclude ".cache/**"
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
	withJavadocJar()
}
tasks.named("javadoc", Javadoc) {
	options.addFileOption('-add-stylesheet', project.file("javadoc-stylesheet.css"))

	options {
		links(
				"https://maven.fabricmc.net/docs/fabric-api-${project.fapi_version}/",
				"https://maven.fabricmc.net/docs/yarn-${project.yarn_version}/"
		)
	}
}

jar {
	from("${rootProject.projectDir}/LICENSE") {
		rename { "${it}" }
	}
}

modrinth {
	// Main properties
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "loot-table-modifier"
	gameVersions = outlet.mcVersions()
    loaders = ["fabric"]

	// Version stuff
	def customVersionName = System.getenv("VERSION_NAME")
	if (customVersionName != null) versionName = customVersionName

	versionNumber = "${project.version}"

    versionType = Boolean.parseBoolean(System.getenv("VERSION_IS_PRERELEASE")) ? "beta" : "release"

    if (rootProject.mod_version.contains("beta")) versionType = "beta"
    if (rootProject.mod_version.contains("alpha")) versionType = "alpha"


	// Files
	uploadFile = remapJar.archiveFile
	additionalFiles = [sourcesJar.archiveFile]


    // Project info
	syncBodyFrom = rootProject.file("README.md").text
	def changelogEnv = System.getenv("VERSION_CHANGELOG")
	if (changelogEnv != null) changelog = changelogEnv


	dependencies {
		required.project "fabric-api"
	}
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)


publishing {
	repositories {
		maven {
			name = "OffsetMonkey538"
			url = "https://maven.offsetmonkey538.top/releases"
			credentials {
				username = providers.gradleProperty("OffsetMonkey538Username").getOrElse(System.getenv("MAVEN_USERNAME"))
				password = providers.gradleProperty("OffsetMonkey538Password").getOrElse(System.getenv("MAVEN_PASSWORD"))
			}
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
	publications {
		maven(MavenPublication) {
			artifactId = base.archivesName.get()

			from(components["java"])
		}
	}
}
