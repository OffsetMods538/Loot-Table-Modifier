plugins {
    id 'multiloader-root'
    id 'java'
    id 'java-library'
    id 'net.neoforged.moddev'           version "${neoforged_moddev}"            apply false
    id 'fabric-loom'                    version "${fabric_loom}"                 apply false
    id 'xyz.jpenilla.resource-factory'  version "${jpenilla_resource_factory}"   apply false
    id 'io.github.dexman545.outlet'     version "${dexman_outlet}"               apply false
    id 'com.modrinth.minotaur'          version "${modrinth_minotaur}"           apply false
}

allprojects {
    apply plugin: "java-library"

    repositories {
        mavenCentral()
        mavenLocal()
        exclusiveContent {
            forRepositories(
                    maven {
                        name = "OffsetMods538"
                        url = "https://maven.offsetmonkey538.top/releases"
                    },
                    mavenLocal()
            )
            filter {
                includeGroupAndSubgroups "top.offsetmonkey538"
            }
        }
    }

    dependencies {
        compileOnlyApi "org.jspecify:jspecify:${rootProject.jspecify_version}"
    }
}

configure(subprojects.findAll { it.hasProperty("project_name") }) {
    apply plugin: "maven-publish"

    publishing {
        repositories {
            maven {
                name = "OffsetMonkey538"
                url = "https://maven.offsetmonkey538.top/releases"
                credentials {
                    username = providers.gradleProperty("OffsetMonkey538Username").getOrElse(System.getenv("MAVEN_USERNAME"))
                    password = providers.gradleProperty("OffsetMonkey538Password").getOrElse(System.getenv("MAVEN_PASSWORD"))
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            register("maven", MavenPublication) {
                afterEvaluate {
                    artifactId base.archivesName.get()
                }
                groupId "top.offsetmonkey538.loottablemodifier"
                from components.java
            }
        }
    }
}

